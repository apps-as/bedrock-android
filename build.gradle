buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.kotlinGradlePlugin
        classpath Libs.dependenciesGradlePlugin
        classpath Libs.mavenPublishGradlePlugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.github.ben-manes.versions"

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            def exclusions = []
            switch (selection.candidate.group) {
                case 'com.android.tools.build' : exclusions = ['alpha']; break
                case 'com.android.tools.lint' : exclusions = ['alpha']; break
                case 'org.jetbrains.kotlinx' : exclusions = ['eap', '1.4-M1', '1.4-M2']; break
                case 'androidx.core' : exclusions = ['alpha']; break
                case 'androidx.lifecycle' : exclusions = ['alpha']; break
            }
            boolean rejected = exclusions.any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}