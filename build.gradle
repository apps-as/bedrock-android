buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.kotlinGradlePlugin
        classpath Libs.dependenciesGradlePlugin
        classpath Libs.mavenPublishGradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "s3://no.apps.maven.s3.amazonaws.com"
            credentials(AwsCredentials) {
                accessKey rootProject.properties["maven_access"]
                secretKey rootProject.properties["maven_secret"]
            }
        }
    }
}

apply plugin: "com.github.ben-manes.versions"

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            def exclusions = []
            switch (selection.candidate.group) {
                case 'com.android.tools.build' : exclusions = ['alpha']; break
                case 'com.android.tools.lint' : exclusions = ['alpha']; break
                case 'org.jetbrains.kotlinx' : exclusions = ['eap']; break
            }
            boolean rejected = exclusions.any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}